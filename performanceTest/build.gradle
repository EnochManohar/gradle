apply from: 'src/generator.groovy'
apply plugin: 'base'

repositories {
    mavenCentral()
}

configurations {
    junit
}

dependencies {
    junit 'junit:junit:4.8.2'
    groovy localGroovy()
}

task small(type: GeneratorTask, description: 'Generates a small project') {
}

task largeSrc(type: GeneratorTask, description: 'Generates a single project with lots of source files') {
    sourceFiles = 50000
    linesOfCodePerSourceFile = 20
}

task multi(type: GeneratorTask, description: 'Generates a multi-project build') {
    projects = 25
    sourceFiles = 100
}

task mixedSize(type: GeneratorTask) {
    projects = 400
    sourceFiles = 100
    projects[1].sourceFiles = 20000
}

task multiGroovy(type: GeneratorTask, description: 'Generates a multi-project groovy build') {
    projects = 25
    groovyProject = true
}

def generators = tasks.withType(GeneratorTask)
generators.all {
    group = 'Project setup'
    testDependencies = configurations.junit
}
task all(dependsOn: generators)
